//
// This file is to be applied to every subproject.
//

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'artifactory'
apply plugin: 'jacoco'

String mavenGroupId = 'hello'
String mavenVersion = '1.0-SNAPSHOT'

sourceCompatibility = '1.7'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

dependencies {
    // Adding dependencies here will add the dependencies to each subproject.
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.5'
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.5.1'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito', version: '1.5.1'
    
    jacocoAgent 'org.jacoco:org.jacoco.agent:0.7.0.201403182114'           
}

String mavenArtifactId = name

group = mavenGroupId
version = mavenVersion

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

task createFolders(description: 'Creates the source folders if they do not exist.') doLast {
    sourceSets*.allSource*.srcDirs*.each { File srcDir ->
        if (!srcDir.isDirectory()) {
            println "Creating source folder: ${srcDir}"
            srcDir.mkdirs()
        }
    }
}

test {
    jacoco {
        excludes = ['**/.*Test']
    }
}